<h1><a id="apigateway">Deploying the API Gateway</a></h1>
<p>
    The work done to this point creates the global Octopus resources required to support a deployment of applications and infrastructure to AWS. With this done we are now able to deploy the API Gateway instance that exposes our Lambda microservices.
</p>
<p>
    The API Gateway provides a shared entry point for web clients interacting with the microservices that make up the Octopub sample application. Because it is a shared resource, it is deployed as a separate project in its own CloudFormation stack. We'll make use of CloudFormation output variables to allow subsequent AWS resources to attach themselves to the API Gateway.
</p>
<p>
    To create the API Gateway deployment project in Octopus, apply the Terraform configuration in the <strong>terraform/apigateway/octopus</strong> directory using the following commands:
</p>
<pre>cd terraform/apigateway/octopus
terraform init
terraform apply</pre>
<p>
    You will be prompted for the URL of your Octopus instance, the Octopus API key, the ID of the space to apply the configuration to, the AWS access key, and the AWS secret key. Terraform then displays a list of changes and prompts you to apply them. Once the configuration is applied a project called <strong>API Gateway</strong> with a single <strong>Deploy an AWS CloudFormation template</strong> step is created:
</p>
<div><img alt="Octopus environments" src="images/octopus/apigateway.png"/></div>
<p>
    Deploying this project creates an new API Gateway instance in AWS with a number of API Gateway resources exposing the <strong>api</strong> and <strong>health</strong> endpoints, and with a proxy resource matching all other requests made under the root resource:
</p>
<div><img alt="Octopus environments" src="images/octopus/apigatewayaws.png"/></div>
<p>
    The CloudFormation stack responsible for creating the API Gateway exposes a number of output variables. Subsequent Lambda deployments will read these variables to attach the Lambda and front end web app to the API Gateway:
</p>
<div><img alt="Octopus environments" src="images/octopus/apigatewaycfoutputs.png"/></div>
<h2>Breaking down the HCL</h2>
<p>
    The deployment of an API Gateway instance is an example of defining a complex deployment process in Octopus via Terraform. The deployment project and the steps it contains require references to a number of resources created in previous chapters. These are exposed via data lookups.
</p>
<p>
    This data lookup gets the ID of the library variable set. 
</p>
<p>Note the pattern shown here where we skip zero results and take one. This pattern allows us to convert an resource name to an ID.
</p>
<p>
  Also note that many data lookups only expose a partial name field rather than a complete name. This is a consequence of the Octopus API, which often allows resource collections to be filtered by a text field in the UI that that return all resources partially matching a name rather than matching the exact text typed in. Performing a lookup on a partial name match will work well most of the time so long as the resource names are unambiguous, but there are edge cases where partial name matching may produce unexpected results:
</p>
<pre>data "octopusdeploy_library_variable_sets" "library_variable_set_octopub" {
    ids          = null
    partial_name = "Octopub"
    skip         = 0
    take         = 1
  }</pre>
<p>
    This data lookup gets the ID of the default lifecycle that is present by default in a new space:
</p>
<pre>data "octopusdeploy_lifecycles" "default" {
    ids          = []
    partial_name = "Default Lifecycle"
    skip         = 0
    take         = 1
  }</pre>
<p>
    This data lookup gets the Linux dynamic worker that is present by default in a new space on a hosted Octopus instance.
</p>
<p> 
  Note that on a self hosted instance of Octopus, this lookup needs to be modified to look up the default worker pool:
</p>
<pre>data "octopusdeploy_worker_pools" "workerpool_hosted_ubuntu" {
    name = "Hosted Ubuntu"
    ids  = null
    skip = 0
    take = 1
  }</pre>
<p>
    Here we create a project group to hold our deployment project:
</p>
<pre>resource "octopusdeploy_project_group" "project_group_infrastructure" {
    name        = "Infrastructure"
    description = "Builds the API Gateway."
  }</pre>
<p>
    Next we define the project group. Note how we use the results of the data lookups against the lifecycles and library variable sets to link them to the deployment project:
</p>  
<pre>resource "octopusdeploy_project" "project_api_gateway" {
    name                                 = "API Gateway"
    auto_create_release                  = false
    default_guided_failure_mode          = "EnvironmentDefault"
    default_to_skip_if_already_installed = false
    description                          = "Deploys a shared API Gateway. This project is created and managed by the [Octopus Terraform provider](https://registry.terraform.io/providers/OctopusDeployLabs/octopusdeploy/latest/docs). The Terraform files can be found in the [GitHub repo](https://github.com/mcasperson/SalesEngineeringAwsLambda)."
    discrete_channel_release             = false
    is_disabled                          = false
    is_version_controlled                = false
    lifecycle_id                         = "${data.octopusdeploy_lifecycles.default.lifecycles[0].id}"
    project_group_id                     = "${octopusdeploy_project_group.project_group_infrastructure.id}"
    included_library_variable_sets       = ["${data.octopusdeploy_library_variable_sets.library_variable_set_octopub.library_variable_sets[0].id}"]
    tenanted_deployment_participation    = "Untenanted"
  
    connectivity_policy {
      allow_deployments_to_no_targets = false
      exclude_unhealthy_targets       = false
      skip_machine_behavior           = "SkipUnavailableMachines"
    }
  }</pre>
<p>
    Finally we define the deployment process, which defines the steps inside the deployment project:
</p>
<pre>resource "octopusdeploy_deployment_process" "deployment_process_project_api_gateway" {
    project_id = "${octopusdeploy_project.project_api_gateway.id}"</pre>
<p>
    Steps contain one or more actions. Steps with more than one action appear as a parent step in the UI, allowing each action to be run in parallel or in a rolling update across targets. Steps with one action appear as regular steps in the UI:
</p>  
<pre>  step {
      condition           = "Success"
      name                = "Create API Gateway"
      package_requirement = "LetOctopusDecide"
      start_trigger       = "StartAfterPrevious"</pre>
<p>
    In this case we have a step with a single action deploying a CloudFormation template. This step is executed on a worker from the Ubuntu worker pool by referencing the results of the worker pool data lookup:
</p>  
<pre>    action {
        action_type                        = "Octopus.AwsRunCloudFormation"
        name                               = "Create API Gateway"
        condition                          = "Success"
        run_on_server                      = true
        is_disabled                        = false
        can_be_used_for_project_versioning = false
        is_required                        = false
        worker_pool_id                     = "${data.octopusdeploy_worker_pools.workerpool_hosted_ubuntu.worker_pools[0].id}"</pre>
<p>
  Steps have a number of common properties, defined above, while the remaining properties are generic key/value pairs. This is referred to as the <strong>property bag</strong>.
</p>
<p>
  There is no documentation describing all the properties that must be defined in the property bag. Typically you would define the step in the Octopus web UI, click the <strong>Download as JSON</strong> option, and then convert the step <strong>Properties</strong> object into the HCL <strong>properties</strong>:
</p>
<div><img alt="Octopus accounts" src="images/octopus/downloadasjson.png"/></div>
<pre>        properties                         = {</pre>
<p>
    A common pattern in step definitions is to assign a JSON blob to a property. It is more readable to represent JSON as a HCL map and encode it with the <strong>jsonenode</strong> function:
</p>
<pre>          "Octopus.Action.Aws.CloudFormation.Tags" = jsonencode([
            {</pre>
<p>
  The template syntax used by Octopus is called <strong>Octostache</strong>. Octostache has a number of filters documented at <a href="https://octopus.com/docs/projects/variables/variable-filters">https://octopus.com/docs/projects/variables/variable-filters</a>. We use the <strong>Replace</strong> filter here to replace anything after a space in the environment name with an empty string. Given our environments are called <strong>Development</strong> and <strong>Production</strong>, this filter does nothing, because these names have no space. However, if we had an environment called <strong>Development (Security)</strong>, this filter would result in a string <strong>Development</strong>:
</p>
<pre>              "value" = "#{Octopus.Environment.Name | Replace \" .*\" \"\"}"
              "key" = "Environment"
            },
            {
              "key" = "DeploymentProject"
              "value" = "API_Gateway"
            },
          ])
          "Octopus.Action.Aws.CloudFormationTemplateParameters" = jsonencode([])
          "Octopus.Action.Aws.Region" = "#{AWS.Region}"
          "Octopus.Action.Aws.CloudFormationStackName" = "#{AWS.CloudFormation.ApiGatewayStack}"</pre>
<p>
    Another common pattern is for step properties to have long text content assigned to them. Heredoc strings allow this text to be defined over multiple lines, improving the readability of the HCL. Here we have a CloudFormation YAML blob written as a heredoc string:
</p>
<pre>          "Octopus.Action.Aws.CloudFormationTemplate" = <<-EOT
          Resources:</pre>
<p>
  The first resource is the API Gateway instance.
</p>
<pre>            RestApi:
              Type: 'AWS::ApiGateway::RestApi'
              Properties:
                Description: My API Gateway
                Name: Octopub
                BinaryMediaTypes:
                  - '*/*'
                EndpointConfiguration:
                  Types:
                    - REGIONAL</pre>
<p>
  API Gateway resources are the individual URL path elements to which Lambdas and other integrations can be attached. We define three resources:
</p>
<ul>
  <li>Health, which exposes health check endpoints.</li>
  <li>Api, which exposes the API endpoints.</li>
  <li>Web, which exposes the frontend web application.</li>
</ul>
<pre>            Health:
              Type: 'AWS::ApiGateway::Resource'
              Properties:
                RestApiId:
                  Ref: RestApi
                ParentId:
                  'Fn::GetAtt':
                    - RestApi
                    - RootResourceId
                PathPart: health
            Api:
              Type: 'AWS::ApiGateway::Resource'
              Properties:
                RestApiId:
                  Ref: RestApi
                ParentId:
                  'Fn::GetAtt':
                    - RestApi
                    - RootResourceId
                PathPart: api
            Web:
              Type: 'AWS::ApiGateway::Resource'
              Properties:
                RestApiId: !Ref RestApi
                ParentId: !GetAtt
                  - RestApi
                  - RootResourceId
                PathPart: '{proxy+}'</pre>
<p>
  The IDs of the resources created by this CloudFormation template are exposed as outputs. These outputs are captured as Octopus output variables able to be consumed in subsequent steps. They are also able to be queried via the <strong>aws</strong> CLI in other Octopus deployment projects:
</p>
          Outputs:
            RestApi:
              Description: The REST API
              Value: !Ref RestApi
            RootResourceId:
              Description: ID of the resource exposing the root resource id
              Value:
                'Fn::GetAtt':
                  - RestApi
                  - RootResourceId
            Health:
              Description: ID of the resource exposing the health endpoints
              Value: !Ref Health
            Api:
              Description: ID of the resource exposing the api endpoint
              Value: !Ref Api
            Web:
              Description: ID of the resource exposing the web app frontend
              Value: !Ref Web
          EOT
          "Octopus.Action.Aws.TemplateSource" = "Inline"
          "Octopus.Action.Aws.WaitForCompletion" = "True"
          "Octopus.Action.Aws.AssumeRole" = "False"</pre>
<p>
  The variable referenced here is defined in the library variable set. Typically, referencing a variable requires the use of the Octostache syntax (i.e. <strong>#{Variable.Name}</strong>), but some fields, like account selection fields, reference the variable names directly:
</p>
<pre>          "Octopus.Action.AwsAccount.Variable" = "AWS.Account"
          "Octopus.Action.AwsAccount.UseInstanceRole" = "False"
        }
        environments                       = []
        excluded_environments              = []
        channels                           = []
        tenant_tags                        = []
        features                           = []
      }
  
      properties   = {}
      target_roles = []
    }
  }</pre>

<p>
    With the API Gateway configured, the next step is to deploy the backend REST API microservice as a Lambda.
</p>
